
if(NOT "@VCPKG_LIBRARY_LINKAGE@" STREQUAL "static")
  message(FATAL_ERROR "Only static linkage is) supported for boringssl-custom")
endif()

include(CMakeFindDependencyMacro)
find_dependency(Threads REQUIRED)

if(NOT TARGET boringssl::crypto OR NOT TARGET boringssl::decrepit)
  add_library(boringssl::crypto UNKNOWN IMPORTED)
  add_library(boringssl::decrepit UNKNOWN IMPORTED)

  if(NOT "@VCPKG_LIBRARY_LINKAGE@" STREQUAL "static")
    message(FATAL_ERROR "Only static linkage is supported for boringssl-custom")
  endif()

  function(boringssl_set_target_libs)
    set(options)
    set(oneValueArgs TARGET)
    set(multiValueArgs NAMES)
    cmake_parse_arguments(arg_set_target_libs "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    set(targetShortName "${arg_set_target_libs_TARGET}")
    set(targetName "boringssl::${arg_set_target_libs_TARGET}")

    set_target_properties(
      ${targetName}
      PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/include"
      IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    )

    find_library(
      ${targetShortName}_libPath
      NAMES ${arg_set_target_libs_NAMES}
      PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/lib"
      NO_DEFAULT_PATH
      REQUIRED
    )
    set_property(TARGET ${targetName} APPEND PROPERTY IMPORTED_CONFIGURATIONS "Release")
    set_target_properties(${targetName} PROPERTIES IMPORTED_LOCATION_RELEASE "${${targetShortName}_libPath}")
    find_library(
      ${targetShortName}_libPath_debug
      NAMES ${arg_set_target_libs_NAMES}
      PATHS "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug/lib"
      NO_DEFAULT_PATH
    )

    if(EXISTS "${${targetShortName}_libPath_debug}")
      set_property(TARGET ${targetName} APPEND PROPERTY IMPORTED_CONFIGURATIONS "Debug")
      set_target_properties(${targetName} PROPERTIES IMPORTED_LOCATION_DEBUG "${${targetShortName}_libPath_debug}")
    endif()

  endfunction()

  boringssl_set_target_libs(TARGET crypto NAMES libbscrypto bscrypto crypto)
  boringssl_set_target_libs(TARGET decrepit NAMES libdecrepit decrepit)

endif()